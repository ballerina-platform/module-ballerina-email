/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - Ballerina Email Connector'

def packageName = "email"
def packageOrg = "ballerina"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def dependenciesTomlFile = new File("$project.projectDir/Dependencies.toml")
def compilerPluginTomlFile = new File("$project.projectDir/CompilerPlugin.toml")
def originalBallerinaToml = ballerinaTomlFile.text
def originalDependenciesToml = dependenciesTomlFile.text
def originalCompilerPluginToml = compilerPluginTomlFile.text

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'com.sun.mail', name: 'javax.mail', version: "${javaMailVersion}") {
        transitive = false
    }
    externalJars(group: 'com.icegreen', name: 'greenmail', version: "${greenmailVersion}") {
        transitive = false
    }
    externalJars(group: 'org.testng', name: 'testng', version: "${testngVersion}") {
        transitive = false
    }
    externalJars(group: 'org.jvnet.mimepull', name: 'mimepull', version: "${mimepullVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def stdlibDependentTaskVersion = stripBallerinaExtensionVersion(project.stdlibTaskVersion)
        def stdlibDependentMimeVersion = stripBallerinaExtensionVersion(project.stdlibMimeVersion)
        def stdlibDependentFileVersion = stripBallerinaExtensionVersion(project.stdlibFileVersion)
        def stdlibDependentTimeVersion = stripBallerinaExtensionVersion(project.stdlibTimeVersion)
        def stdlibDependentLogVersion = stripBallerinaExtensionVersion(project.stdlibLogVersion)
        def stdlibDependentOsVersion = stripBallerinaExtensionVersion(project.stdlibOsVersion)
        def stdlibDependentIoVersion = stripBallerinaExtensionVersion(project.stdlibIoVersion)
        def newBallerinaToml = ballerinaTomlFile.text.replace("@project.version@", project.version)
        newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
        ballerinaTomlFile.text = newBallerinaToml

        def newDependenciesToml = dependenciesTomlFile.text.replace("@stdlib.task.version@", stdlibDependentTaskVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.mime.version@", stdlibDependentMimeVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.file.version@", stdlibDependentFileVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.time.version@", stdlibDependentTimeVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.log.version@", stdlibDependentLogVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.os.version@", stdlibDependentOsVersion)
        newDependenciesToml = newDependenciesToml.replace("@stdlib.io.version@", stdlibDependentIoVersion)
        dependenciesTomlFile.text = newDependenciesToml

        def newCompilerPluginToml = compilerPluginTomlFile.text.replace("@project.version@", project.version)
        compilerPluginTomlFile.text = newCompilerPluginToml
    }
}

task revertTomlFiles {
    doLast {
        ballerinaTomlFile.text = originalBallerinaToml
        dependenciesTomlFile.text = originalDependenciesToml
        compilerPluginTomlFile.text = originalCompilerPluginToml
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs

build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
build.dependsOn ":${packageName}-compiler-plugin:build"
build.dependsOn ":${packageName}-test-utils:build"
test.dependsOn ":${packageName}-native:build"
test.dependsOn ":${packageName}-compiler-plugin:build"
test.dependsOn ":${packageName}-test-utils:build"

publishToMavenLocal.dependsOn build
publish.dependsOn build
